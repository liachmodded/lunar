plugins {
  id 'java'
  id 'maven'
  id 'signing'
  id 'net.minecrell.licenser' version '0.3'
}

group 'net.kyori'
version '1.0.6'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

configurations {
  checkerFrameworkCheckerJar {
    description = 'the Checker Framework, including the Type Annotations compiler'
  }

  checkerFrameworkAnnotatedJDK {
    description = 'a copy of JDK classes with Checker Framework type qualifers inserted'
  }
}

ext.checkerFrameworkVersion = '2.5.1'

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile 'com.google.guava:guava:21.0'
  testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.3'
  testCompile 'org.junit.jupiter:junit-jupiter-engine:5.0.3'
// Checker Framework from Maven Central
  checkerFrameworkAnnotatedJDK "org.checkerframework:jdk8:${checkerFrameworkVersion}"
  checkerFrameworkCheckerJar "org.checkerframework:checker:${checkerFrameworkVersion}"
  implementation "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
}

tasks.withType(JavaCompile) {
  options.compilerArgs += [
    '-Xlint:all',
    '-Xlint:-path',
    '-parameters',
    '-processorpath', "${configurations.checkerFrameworkCheckerJar.asPath}",
    '-processor', 'org.checkerframework.checker.nullness.NullnessChecker',
    '-Xmaxerrs', '10000',
    '-Xmaxwarns', '10000',
    '-Awarns',
    '-AcheckPurityAnnotations',
    "-Xbootclasspath/p:${configurations.checkerFrameworkAnnotatedJDK.asPath}"
  ]
  options.deprecation = true
  options.encoding = 'UTF-8'
}

license {
  header project.file('header.txt')
  include '**/*.java'
  newLine false
}

task javadocJar(type: Jar) {
  classifier 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

signing {
  required { !project.version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(':uploadArchives') && project.hasProperty('signing.keyId') }
  sign configurations.archives
}

jar {
  manifest.attributes(
    'Automatic-Module-Name': 'net.kyori.lunar'
  )
}

if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
  uploadArchives {
    enabled = System.getenv('TRAVIS') == null || project.version.endsWith('-SNAPSHOT')

    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
          description 'A collection of utilities.'
          name project.name
          url 'https://github.com/KyoriPowered/lunar/'

          developers {
            developer {
              name 'kashike'
            }
          }

          issueManagement {
            system 'GitHub Issues'
            url 'https://github.com/KyoriPowered/lunar/issues'
          }

          licenses {
            license {
              name 'MIT License'
              url 'https://opensource.org/licenses/MIT'
            }
          }

          scm {
            connection 'scm:git@github.com:KyoriPowered/lunar.git'
            developerConnection 'scm:git@github.com:KyoriPowered/lunar.git'
            url 'https://github.com/KyoriPowered/lunar/'
          }
        }
      }
    }
  }
}
